
;; indicies in the array building-list
globals [
 building-list     ;; read from input file, then tweaked.
 background-colours ;; preserve colours of patches behind a building.
 
 I-ID              ;;  0  - ID of the building
 I-Name            ;;  1
 I-NumWorkers      ;;  6 - total number of workers starting from this building
 I-Dest            ;;  7  - workers in this building are going towards one of these buildings (this item is a list)
 I-MudSlows        ;;  8 - bold workers will be slowed down in the mud
 I-Harm            ;; 10 - workers in this building are harmed if they enter the danger zone
 I-Center          ;; 15 - the center patch - do not keep looking up the array and calculating this.
 I-Radius          ;; 16 - limit the worker destinations to a circle in the middle.
 I-CoveredColours  ;; 17 - record all of the colours that were on these patches before the building was made.
]

; 
; setup-building-index-values
;
; Create a list of meaningful values for indexes in building-list
;
to setup-building-index-values
  set I-ID              0
  set I-Name            1
  set I-NumWorkers      6
  set I-Dest            7
  set I-MudSlows        8
  set I-Harm           10
  set I-Center         15
  set I-Radius         16
  set I-CoveredColours 17
end

; 
; create-buildinglist
;
; reads the xlsx file, creates an array of buildings
;
to create-buildinglist
    let temp-building-list csv:from-file "buildings.csv" 
    set background-colours [] 
    
    let firstrow item 0 temp-building-list
    if item 0 firstrow = "facility" [ set temp-building-list remove-item 0 temp-building-list]

    foreach temp-building-list [
      let buildnum  item I-ID ?
      let dest item I-Dest ?
      let mud-slows 0
      if item 8 ? > 0 or item 9 ? > 0 
      [ set mud-slows 1 ]
      let danger-harms item 10 ?
      
      ;; check destination(s) for a single value or a list
      ;; currently, this only works for two destinations.  need to loop for more than two
      ifelse (member? "," (word dest)) [
        let comma position "," dest
        let desta read-from-string substring dest 0 comma
        let destb read-from-string substring dest (comma + 1) (length dest)
        set dest list desta destb
      ]  [
        set dest (list dest)
      ]
      
      ;; update the building array
      let new-building replace-item I-Dest ? dest
      set new-building lput patch item 2 ? item 3 ? new-building  ;; index 13 is the center of the building, and we will reset this when the building is created/moded
      let worker-zone (min (list item 4 ? item 5 ? ) / 2 ) - 2
      set new-building lput worker-zone new-building  ;; index 14  ;; keep workers in this radius around the center

      set building-list lput new-building building-list
    ]
    
end



to-report building-stats [building-num]
    let bx1 min [pxcor] of patches with [building-number = building-num]
    let by1 min [pycor] of patches with [building-number = building-num]
    let bwid max [pxcor] of patches with [building-number = building-num] - bx1
    let blen max [pycor] of patches with [building-number = building-num] - by1

    let avoiders count turtles with [avoid-mud? = 1 and home-building = building-num]
    let bold count turtles with [avoid-mud? = 0 and home-building = building-num]
    let avoiders-trips  mean [trips-completed] of  turtles with [avoid-mud? = 1 and home-building = building-num]
    let bold-trips  mean [trips-completed] of  turtles with [avoid-mud? = 0 and home-building = building-num]
    let avoiders-triptime 0
    if avoiders-trips > 0 [ set avoiders-triptime total-ticks / avoiders-trips ]
    let bold-triptime 0
    if bold-trips > 0 [ set bold-triptime  total-ticks / bold-trips]

    let napper-trips 0
    set napper-trips mean  [no-energy-tick] of workers with [no-energy-tick > 0 and building-num = home-building]

    report (list building-num bx1 by1 bwid blen avoiders bold avoiders-trips bold-trips avoiders-triptime bold-triptime napper-trips)
end

;;
;; center-of
;;
;; find the building info, return the center
;;
to-report center-of [ building-num]
  foreach building-list [
    if building-num = item 0 ? [
      if not is-patch? item 15 ? [
        report reset-center-of-building building-num
      ]
      report item 15 ?
    ]
  ]
  
  ;; 1 doesn't appear in the list, just look it up.
  if building-num = 1
  [
      report reset-center-of-building building-num
  ]
  
  ;; this should not be possible.  but just in case.
  report patch 1 1
end

;;
;; worker-radius
;;
;; find the building info, return the radius where workers should start
;;
to-report shoot-a-dart [ building-num]
  let theradius 15
  let thedart one-of patches with [building-number = building-num]
  
  ;; look up the radius
  foreach building-list [
    if building-num = item 0 ? [
      set theradius item 16 ?
      
    ]
  ]
  
  ;; find the center, ask a patch near the center to volunteer
  sprout 1  [
    move-to center-of building-num
    set thedart one-of patches in-radius theradius
    die
  ]
  
  report thedart
    
end

;;
;; find-center-of-building
;;
;; look on map to see building center of current building location, update the array of building info
;;
to-report reset-center-of-building [building-num]
;; another option is nearest block in building    
;; min-one-of patches with [ building-number = [destination-building] of myself ] [ distance myself ]
    let bx1 min [pxcor] of patches with [building-number = building-num]
    let by1 min [pycor] of patches with [building-number = building-num]
    let bx2 max [pxcor] of patches with [building-number = building-num]
    let by2 max [pycor] of patches with [building-number = building-num]
    let bxcenter mean list bx1 bx2
    let bycenter mean list by1 by2

    let ndx 0
    foreach building-list [
      if building-num = item 0 ? [
            let new-building replace-item 15  ? patch bxcenter bycenter  ;; index 15 is the center of the building, and we will reset this when the building is created/moded
            set building-list replace-item ndx building-list new-building
      ]
      set ndx ndx + 1
    ]
    
    report patch bxcenter bycenter
end


;;
;; move-building
;;
;; If the UI button "move building" is down, respond to a mouse click. 
;; Move the bottom left corner of the building to the point where the mouse was clicked.  Replace with "path". 
;;
to move-building
  
  ;; respond after a mouse click is released
  if mouse-down? [ set mouse-was-down? true ]
  if mouse-was-down? = true and not mouse-down? [
    set mouse-was-down? false 
 
    without-interruption [
         let temp-building-width 20
         let temp-building-height 20

         ;; grab the building size
         ask patches with [building-number = building-to-move] [
           set temp-building-width  building-width
           set temp-building-height building-height
         ]
         
         if not building-fits? mouse-xcor mouse-ycor temp-building-width temp-building-height
         [ show "The building will not fit here."
           stop ]

         ;; clear the old building
         restore-background building-to-move

         ;; create the new building
         let x2  mouse-xcor + temp-building-width
         let y2   mouse-ycor + temp-building-height
         construct-hovel building-to-move mouse-xcor mouse-ycor x2 y2
         
         ;; delete old breadcrumbs, make new ones.  People too, we need them cleared to make breadcrumbs.
         ask workers [ die ]
         set breadcrumb-trails []
         create-breadcrumbs-from_buildinglist
         setup-workers

         ;; before execution, move workers to their building.  If already executing, reset their directions.
         ifelse ticks = 0 [
           ask workers with [home-building = building-to-move][
              move-to one-of patches with [building-number = building-to-move]
           ]
         ][
            redirect-workers
         ]
         stop
       ]
  ]

end


;;
;; restore-background
;;
;; Set the building patches to the colours in the preserved list 
;;
to restore-background [building-num]
  foreach background-colours 
  [
    if item 0 ? = building-num
    [
      let background item 1 ?
      foreach background
      [
        ask patch item 0 ? item 1 ? 
        [ 
          set pcolor item 2 ? 
          set building-number 0
        ]
      ]
      stop
    ]
  ]
end

;;
;; construct-hovel
;;
;; Set up the given range of patches to be a building, and add the required information. 
;;
to construct-hovel [building-num x1 y1 x2 y2]
  without-interruption
  [
    
   let background [] 
   ask patches with [pxcor >= x1 and pxcor <= x2 and  pycor >= y1 and pycor <= y2]
   [
     set background lput (list pxcor pycor pcolor) background
     set pcolor  clr-moveable-facility
     set building-number  building-num
     set building-width x2 - x1
     set building-height y2 - y1
   ]
   preserve-background  building-num background 
 
   let center reset-center-of-building building-num  ;; find it, update the record

   ask patches with [pxcor = x1 + 5 and pycor = y1 + 5]
   [
     set plabel-color black
     set plabel building-num
   ]
  ]
end

;;
;; preserve-background
;;
;; For each building ID, add a list of background colours.  If the building is already in the list, replace it. 
;;
to preserve-background [ building-num background ]
  let index 0
  if is-list? background-colours [
    foreach background-colours 
    [
      if item 0 ? = building-num
      [
        set background-colours replace-item index background-colours (list building-num background)
        stop
      ]
      set index index + 1
    ]
  ]
  
  ;; if we got to here, this building is not in the list
  set background-colours lput (list building-num background) background-colours
end


to-report work-time [building-num]
  foreach building-list [
    if building-num = item 0 ? [
      let min-time item 11 ?
      let range item 12 ? - item 11 ?
      let the-time random (range) + min-time
      report the-time
    ]
  ]

  report 0
end


;;
;; draw-mainbuilding
;;
;; The position of building 1 is hard-coded, and cannot move.
;;
to draw-mainbuilding 
  ask patches with [pxcor >= 71 and pycor >= 108 and pxcor <= 114 and pycor <= 149]
  [set pcolor clr-fixed-facility
    set building-number 1]   
end

;;
;; make-buildings-from-list
;;
;; Check out the list, create the buildings.  Caled from Startup
;;
to  make-buildings-from-list[  ]
  foreach building-list [
    let buildnum  item 0 ?
    if buildnum != "facility" [
      let buildname  item 1 ?
      let x1  item 2 ?
      let y1  item 3 ?
      let width  item 4 ?
      let height  item 5 ?
      let x2  x1 + width
      let y2  y1 + height

      construct-hovel buildnum x1 y1 x2 y2
    ]
  ]
  draw-mainbuilding 
  
end


;;
;; building-fits?
;;
;; Check the patches here, see if a building is allowed in all of them.
;;
to-report building-fits? [thex they width height ]
  let itfits? true
  ask patches with [pxcor >= thex and pxcor <= thex + width and pycor >= they and pycor  <= they + height]
  [
    if not member? pcolor  (list clr-path clr-mud clr-road clr-danger ) [ set itfits? false ]
  ]
  
  report itfits?
end
