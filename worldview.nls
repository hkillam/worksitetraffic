;;
;; World View
;;
;; import the bitmap, match colours and path types, save the bitmap, stuff like that.
;;



globals [
  clr-path
  clr-slowmud
  clr-tiredmud
  clr-road
  clr-danger
  clr-moveable-facility
  clr-fixed-facility
  clr-fixed-facility2
  clr-void
  
  ;; obsticle behavioiurs - read from colour file.
  obdanger-speed
  obdanger-energy
  obdanger-injury
  obslow-speed
  obslow-energy
  obslow-injury
  obtired-speed
  obtired-energy
  obtired-injury
  
  ;; indicies in the array read from the colour file
  I-title           ;; 0 - will match our constant clr- values
  I-clrnum          ;; 1 - netlogo colour ID
  I-rgb             ;; 2 - rgb value for the colour
  I-readablename    ;; 3 - an easy to read name - not used
  I-speed           ;; 4 - steps taken in single tick - default is 1
  I-energy          ;; 5 - energy used in single tick - normal is 1
  I-injury          ;; 6 - percent chance of injury on trip through an obssticle.  normal is 0


]


patches-own [
  building-number   ;; number (14, 15, or 16) to identify the material sources
  building-width    ;;
  building-height   ;;  for moving and rebuilding.
]


;;
;; setup-colours
;;
;; give the colour constants values before reading the colour file
;;
to setup-colours
  ;; default colours
  set clr-path 39   ; beige
  set clr-road 37   ; med brown
  set clr-slowmud 35    ; light brown
  set clr-tiredmud 47  ;  yellow
  set clr-danger 27 ; oranage
  set clr-moveable-facility 55  ;green
  set clr-fixed-facility 122 ; dark purple
  set clr-fixed-facility2 127 ; lighter purple
  set clr-void  0    ;  black, out of the map

  ;; behaviours in the obsticle patches
  set   obdanger-speed      1
  set   obdanger-energy     1
  set   obdanger-injury     2
  set   obslow-speed        0.5
  set   obslow-energy       1
  set   obslow-injury       0
  set   obtired-speed       1
  set   obtired-energy      2
  set   obtired-injury      0
  
  ;; indicies in the colour file/list
  set I-title   0
  set I-clrnum  1         ;; 1 - netlogo colour ID
  set I-rgb     2         ;; 2 - rgb value for the colour
  set I-readablename    3         ;; 3 - an easy to read name - not used
  set I-speed   4         ;; 4 - steps taken in single tick - default is 1
  set I-energy  5         ;; 5 - energy used in single tick - normal is 1
  set I-injury  6         ;; 6 - chance of injury on trip through an obssticle.  normal is 0
  
end


;;
;; add-blocks
;;
;; If the button for "add blocks" is on, this function watches for mouse clicks, and 
;; changes the color of the patches to the selected one.  It will not change buildings or space
;; outside of the construction zone.
;;
to add-blocks
  let fixedcolors (list clr-moveable-facility clr-fixed-facility clr-fixed-facility2 clr-void)
  every .1
   [ if mouse-down?
     [ ask patch mouse-xcor mouse-ycor [
       ask patches in-radius 3 with [not member? pcolor fixedcolors] [
         if new-path-type = "slowmud"
            [ set pcolor clr-slowmud ]
         if new-path-type = "tiredmud"
            [ set pcolor clr-tiredmud ]
         if new-path-type = "path"
            [ set pcolor clr-path ]
         if new-path-type = "danger"
            [ set pcolor clr-danger ]
        ]
     ]
     ]
   ]
end

;;
;; SaveWorldBitmap
;;
;; Save the current world view as a bitmap, after killing all of the workers.  We don't want the workers to show up in the bitmap.
;;
to SaveWorldBitmap 
  ask workers [ die ]  ;; we don't want them to show in the bitmap
  bitmap:export bitmap:from-view "results-bitmap.bmp"
end


;;
;; read-color-file
;;
;; read values in colours.csv and define our patch values and behaviours
;;
to read-color-file [ colorfile]

  ;; read colourfile
  file-open colorfile
  let  labels csv:from-row file-read-line
  while [ not file-at-end? ] [
    let row csv:from-row file-read-line
    let clrpatch item I-title row
    let clrnum item I-clrnum row
    let clrrgb item I-rgb row
    let clrname item I-name row
    if (clrpatch = "clr-path") [ set clr-path  clrnum ]
    if (clrpatch = "clr-road") [ set clr-road  clrnum ]
    if (clrpatch = "clr-moveable-facility") [ set clr-moveable-facility  clrnum ]
    if (clrpatch = "clr-fixed-facility") [ set clr-fixed-facility  clrnum ]
    if (clrpatch = "clr-fixed-facility2") [ set clr-fixed-facility2  clrnum ]
    if (clrpatch = "clr-void") [ set clr-void  clrnum ]

    ;; obsticle patches also have behaviours, read them too
    if (clrpatch = "clr-slowmud") 
    [ 
      set clr-slowmud  clrnum
      set obslow-speed item I-speed row
      set obslow-energy item I-energy row
      set obslow-injury  item I-injury row
    ]

    if (clrpatch = "clr-tiredmud") 
    [ 
      set clr-tiredmud  clrnum 
      set obtired-speed item I-speed row
      set obtired-energy item I-energy row
      set obtired-injury  item I-injury row
    ]

    if (clrpatch = "clr-danger") 
    [ 
      set clr-danger  clrnum 
      set obdanger-speed item I-speed row
      set obdanger-energy item I-energy row
      set obdanger-injury  item I-injury row
    ]


  ]
  file-close
end

;;
;; setup-patches
;;
;; Read bitmap, draw world.  Make sure that all patches colors are ones we work with - adjust the ones that are not.
;;
to setup-patches [mapfile colorfile]

   read-color-file colorfile

  ;; read image
  let mapcolors (list clr-path clr-road clr-slowmud clr-tiredmud clr-danger clr-moveable-facility clr-fixed-facility clr-fixed-facility2 clr-void)
  bitmap:copy-to-pcolors (bitmap:import mapfile) true
  

  ;; adjust patch colours to match our list
  let extracolors (list -1)
  ask patches
  [
    set building-number 0
    set pcolor round pcolor   ;; adjust the fuzzy patches

    ;; find all the colours that are not one of our scheme, and adjust them to fit.
    if (not member? pcolor mapcolors) [
      if (pcolor <= 4) [ set pcolor clr-void ]
      if (pcolor >= 5 and pcolor <= 9) [ set pcolor clr-path ]
      if (pcolor = 13 ) [set pcolor clr-fixed-facility2]
      if (pcolor >= 17 and pcolor <= 19 ) [ set pcolor clr-path ]
      if (pcolor = 20 ) [ set pcolor clr-void ]
      if (pcolor >= 26 and pcolor <= 29 ) [ set pcolor clr-danger ]
      if (pcolor >= 30 and pcolor <= 35 ) [ set pcolor clr-void ]
      if (pcolor >= 36 and pcolor <= 36 ) [ set pcolor clr-slowmud ]   ;; not possible to sort road/mud here
      ;; 37 is road
      if (pcolor >= 38 and pcolor <= 38 ) [ set pcolor clr-path ]
      if (pcolor = 40 ) [ set pcolor clr-void ]  ;; divisble by 10 is always black, in every hue.  
      if (pcolor >= 44 and pcolor <= 48 ) [ set pcolor clr-tiredmud ]   
      if (pcolor = 50 ) [ set pcolor clr-void ]
      if (pcolor >= 80 and pcolor <= 83 ) [ set pcolor clr-void ]
      if (pcolor >= 90 and pcolor <= 93 ) [ set pcolor clr-void ]
      if (pcolor >= 100 and pcolor <= 105 ) [ set pcolor clr-void ]
      if (pcolor = 105)  [ set pcolor clr-fixed-facility2 ]
      if (pcolor >= 106 and pcolor <= 109 ) [ set pcolor clr-path ]
      if (pcolor >= 110 and pcolor <= 115 ) [ set pcolor clr-void ]
      if (pcolor >= 120 and pcolor <= 123 ) [ set pcolor clr-fixed-facility]
      if (pcolor >= 124 and pcolor <= 128 ) [ set pcolor clr-fixed-facility2 ]
      if (pcolor = 129 ) [ set pcolor clr-path ]
      if (pcolor >= 130 and pcolor <= 134) [ set pcolor clr-fixed-facility2 ]
      if (not member? pcolor extracolors and not member? pcolor mapcolors) [
        ;; show (word "unexpected color " pcolor " at " pxcor "," pycor)
         set extracolors lput pcolor extracolors  ;; did we miss any?
      ]
    ]
  ]
   ;;show (word "Unrecognized colors in bitmap: " extracolors )

end

